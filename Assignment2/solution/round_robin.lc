val newCList = \e.
    let val a = new [] in
    a := [e,a];
    a;;

-- complete the following functions 

val next = \a.
    case !a of {
        [e,a'] -> a'
    };;

val get = \a.
    case !a of {
        [e,a'] -> e 
    };;

val update = \e. \a. 
    a := [e, (next a)];;

val insert = \e. \a.
    let val b = new [] in
    b := [e, (next a)];
    a := [(get a),b];;

val delete = \a.
    a := [(get a), (next(next a))];;

-- After completing the functions above, running
-- `testAll [];;` should produce true for each test.


-- Tests

rec toListAux = \h. \b. case b == h of { 
        true -> [] ,
        false -> case !b of {
            [v, b'] -> v : (toListAux h b')
        } 
};;

val cListToList = \a. case !a of {
    [v, a'] -> v : (toListAux a a') 
};;


-- Tests for the insert function

val testInsert1 = 
    let val l = newCList "a" in
    insert "b" l;
    cListToList l == ["a", "b"]
;;


val testInsert2 = 
    let val l = newCList "a" in
    insert "c" l;
    insert "b" l;
    cListToList l == ["a", "b", "c"]
;;


-- Tests for the delete function

val testDelete1 = 
    let val l1 = new [] in
    let val l2 = new [] in
    let val l3 = new [] in
    l1 := ["a", l2];
    l2 := ["b", l3];
    l3 := ["c", l1];
    delete l1;
    cListToList l1 == ["a", "c"]
;;


val testDelete2 = 
    let val l = newCList "a" in
    delete l;
    cListToList l == ["a"]
;;


-- Tests for the get/next functions

val testNext1 = 
    let val l1 = new [] in
    let val l2 = new [] in
    l1 := ["a", l2];
    l2 := ["b", l1];
    (next l1) == l2
;;

val testNext2 = 
    let val l1 = new [] in
    let val l2 = new [] in
    l1 := ["a", l2];
    l2 := ["b", l1];
    (next (next l1)) == l1
;;

val testGet1 = 
    let val l = newCList "a" in
    (get l) == "a"
;;

val testNextGet1 = 
    let val l1 = new [] in
    let val l2 = new [] in
    l1 := ["a", l2];
    l2 := ["b", l1];
    (get (next l1)) == "b"
;;


-- Tests for the update function

val testUpdate1 = 
    let val l = newCList "a" in
    update "b" l;
    cListToList l == ["b"]
;;

val testUpdate2 = 
    let val l1 = new [] in
    let val l2 = new [] in
    l1 := ["a", l2];
    l2 := ["b", l1];
    update "c" l1;
    cListToList l1 == ["c", "b"]
;;


-- Tests for the round robin algorithm

rec roundRobin = \ts. \a. case ts of {
    [] -> a,
    (0 : ts') -> (
        let val c = get (next a) in
        case c of {
            0 -> delete a ,
            _ -> update (c-1) (next a)
        };
        roundRobin ts' (next a)
    ),
    (t : ts') -> (
        case [a == (next a) , (get a) == 0] of {
            [true,true] -> update t a,
            _ -> insert t a
        };
        roundRobin ts' (next a)
    )
};;

val tick = 0;;

val addTask = \n.n;;

val testRRobin = \ts. cListToList (roundRobin ts (newCList 0));;

val test1 = (testRRobin [tick]) == [0];;

val test2 = (testRRobin [addTask 3, addTask 4, tick, tick]) == [3, 2];;

val test3 = (testRRobin [addTask 6, tick, tick]) == [4];;

val test4 = (testRRobin [addTask 6, tick, tick,addTask 5]) == [5,4];;

val test5 = (testRRobin [addTask 6, tick, tick,addTask 5,tick]) == [3,5];;

val test5 = (testRRobin [addTask 6, tick, tick,addTask 5,tick]) == [3,5];;


val test6 = (testRRobin [addTask 6, tick, tick,addTask 1, tick, tick, tick, tick]) == [2];;

val testAll = \a.
    print "testInsert1 passing..." ; print testInsert1;
    print "testInsert2 passing..." ; print testInsert2;
    print "testDelete1 passing..." ; print testDelete1;
    print "testDelete2 passing..." ; print testDelete2;
    print "testNext1 passing..." ; print testNext1;
    print "testNext2 passing..." ; print testNext2;
    print "testGet1 passing..." ; print testGet1;
    print "testNextGet1 passing..." ; print testNextGet1;
    print "testUpdate1 passing..." ; print testUpdate1;
    print "testUpdate2 passing..." ; print testUpdate2;
    print "test1 passing..." ; print test1;
    print "test2 passing..." ; print test2;
    print "test3 passing..." ; print test3;
    print "test4 passing..." ; print test4;
    print "test5 passing..." ; print test5;
    print "test6 passing..." ; print test6;
    ""
    ;;




